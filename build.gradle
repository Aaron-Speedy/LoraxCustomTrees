plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.6'
    id 'xyz.jpenilla.run-paper' version '2.3.1'
    id "io.papermc.paperweight.userdev" version "2.0.0-beta.17"
}

group = 'org.infotoast'
version = '6.0'

repositories {
    mavenCentral()
    maven {
        name = 'papermc'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'codemc-repo'
        url = uri('https://repo.codemc.org/repository/maven-public/')
    }
    maven {
        name = 'enginehub'
        url = 'https://maven.enginehub.org/repo/'
    }
}

dependencies {
    paperweight.paperDevBundle("1.21.6-R0.1-SNAPSHOT")
    compileOnly ("com.sk89q.worldedit:worldedit-core:7.3.13") {
        exclude group: "org.yaml"
    }
    compileOnly "com.sk89q.worldedit:worldedit-bukkit:7.3.13"
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    toolchain.languageVersion.set(JavaLanguageVersion.of(21))
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks {
    assemble {
        dependsOn(tasks.reobfJar)
    }
    runServer {
        downloadPlugins {
            url("https://cdn.modrinth.com/data/1u6JkXh5/versions/U0uDF7yg/worldedit-bukkit-7.3.13.jar")
        }
        minecraftVersion("1.21.6")
    }
}
